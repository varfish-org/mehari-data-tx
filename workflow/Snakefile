from snakemake.utils import validate, min_version

min_version("8.25.3")


configfile: "config/config.yaml"


validate(config, schema="schemas/config.schema.yaml")


wildcard_constraints:
    assembly=r"(GRCh37|GRCh38)",
    source=r"(refseq|ensembl)",


include: "rules/common.smk"
include: "rules/download.smk"
include: "rules/cdot.smk"
include: "rules/seqrepo.smk"
include: "rules/mehari.smk"
include: "rules/validate.smk"


rule all:
    input:
        "results/all.completed",


rule collect_results:
    input:
        expand(
            [
                "results/{alias}/mehari/seqrepo/txs.bin.zst",
                "results/{alias}/mehari/seqrepo/txs.bin.zst.sha256",
                "results/{alias}/mehari/seqrepo/txs.bin.zst.report.jsonl",
                "results/{alias}/mehari/seqrepo/txs.bin.zst.report.jsonl.sha256",
                # "results/{alias}/mehari/seqrepo/txs.bin.zst.discards.tsv",
                "results/{alias}/mehari/seqrepo/report/mehari_db_check.txt",
            ],
            alias=[
                "GRCh37-refseq",
                "GRCh38-refseq",
                "GRCh38-ensembl",
                "GRCh37-ensembl",
            ],
        ),
        expand(
            [
                "results/{assembly}-ensembl-and-refseq/mehari/seqrepo/txs.bin.zst",
                "results/{assembly}-ensembl-and-refseq/mehari/seqrepo/txs.bin.zst.sha256",
            ],
            assembly=["GRCh37", "GRCh38"],
        ),
    output:
        touch("results/all.completed"),
